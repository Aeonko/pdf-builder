import org.gradle.api.tasks.wrapper.Wrapper.DistributionType

buildscript {
  repositories { jcenter() }
  dependencies {
    classpath "com.netflix.nebula:nebula-bintray-plugin:$nebula_bintray_plugin_version"
    classpath "com.netflix.nebula:nebula-publishing-plugin:$nebula_publishing_plugin_version"
    classpath "com.netflix.nebula:nebula-release-plugin:$nebula_release_plugin_version"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.github.ben-manes:gradle-versions-plugin:$gradle_version_plugin_version"
  }
}

repositories { jcenter() }

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin'
apply plugin: 'nebula.maven-publish'
apply plugin: 'nebula.nebula-bintray-publishing'
apply plugin: 'nebula.nebula-release'
apply plugin: 'nebula.javadoc-jar'
apply plugin: 'nebula.source-jar'
apply plugin: 'org.jetbrains.dokka'

group 'com.github.timrs2998'
description = 'PDF builder written in Kotlin with a statically typed DSL'

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  pkg {
    repo = 'maven'
    userOrg = 'timrs2998'
    licenses = ['GPL-3.0']
    labels = ['pdf', 'jvm', 'java', 'kotlin', 'pdf-builder']
    publicDownloadNumbers = false
    version {
      name = project.version
      vcsTag = project.version
      attributes = [:]
    }
  }
}

dependencies {
  api "org.apache.pdfbox:pdfbox:$apache_pdfbox_version"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  testImplementation("org.spekframework.spek2:spek-dsl-jvm:$spek_version") {
    exclude group: 'org.jetbrains.kotlin'
  }
  testRuntimeOnly("org.spekframework.spek2:spek-runner-junit5:$spek_version") {
    exclude group: 'org.junit.platform'
    exclude group: 'org.jetbrains.kotlin'
  }
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
}

dokka {
  outputFormat = 'javadoc'
  outputDirectory = "$buildDir/javadoc"
  jdkVersion = 6
  linkMapping {
    dir = "src/main/kotlin"
    url = "https://github.com/timrs2998/pdf-builder/blob/master/src/main/kotlin"
    suffix = "#L"
  }
  moduleName = ''
}
tasks.javadoc.dependsOn tasks.dokka


tasks.withType(AbstractArchiveTask) {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
}

compileKotlin {
  kotlinOptions {
//    allWarningsAsErrors = true
  }
}

test {
  useJUnitPlatform {
    includeEngines 'spek2'
  }
}

wrapper {
  distributionType = DistributionType.ALL
  gradleVersion = '5.2.1'
}
